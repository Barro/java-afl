# Copyright 2018  Jussi Judin
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

java_library(
    name = "JavaAflRaw",
    srcs = ["javafl/JavaAfl.java"],
)

genrule(
    name = "javafl-javah",
    srcs = ["javafl/JavaAfl.java"],
    outs = ["javafl/javafl_JavaAfl.h"],
    cmd = """\
javah -d $(@D) -jni javafl.JavaAfl
""",
)

cc_library(
    name = "java-afl-lib",
    srcs = [
        "@local_jdk//:jni_header",
        "@local_jdk//:jni_md_header-linux",
        "JavaAfl.c",
    ],
    hdrs = [
        ":javafl-javah",
    ],
    includes = [
        "external/local_jdk/include",
        "external/local_jdk/include/linux",
        "javafl",
    ],
)

cc_binary(
    name = "libjava-afl.so",
    deps = [ ":java-afl-lib"],
    linkshared = 1,
)

java_library(
    name = "JavaAflInject",
    srcs = ["javafl/JavaAflInject.java"],
    deps = ["@asm//jar"],
)

genrule(
    name = "JavaAfl-injected",
    srcs = [
        ":JavaAflRaw",
        ":JavaAflInject",
        ":libjava-afl.so",
        "@asm//jar",
    ],
    outs = ["javafl/JavaAfl.class"],
    cmd = """\
jar xf $(location JavaAflRaw);
java -cp $(location :JavaAflInject):$(location @asm//jar) \
    javafl.JavaAflInject javafl/JavaAfl.class $(location :libjava-afl.so);
cp javafl/JavaAfl.class $@
""",
)

java_binary(
    name = "java-afl-instrument",
    srcs = [
        "javafl/CustomInit.java",
        "javafl/JavaAflInstrument.java",
        ],
    deps = [
        ":JavaAflRaw",
        "@asm//jar",
    ],
    resources = [
        ":JavaAfl-injected",
    ],
    main_class = "javafl.JavaAflInstrument",
    visibility = ["//visibility:public"],
)
